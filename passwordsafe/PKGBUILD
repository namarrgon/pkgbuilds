# Maintainer: ME
_pkgname=pwsafe
pkgname=passwordsafe
_pkgver=0.93
pkgver="$_pkgver"BETA
pkgrel=1
pkgdesc="Simple & Secure Password Management"
arch=('i686' 'x86_64')
url="http://passwordsafe.sourceforge.net/index.shtml"
license=('Artistic2.0')
#to build without yubikey support, remove ykpers and set NO_YUBI to "1" in build()
depends=('libxtst' 'wxgtk2.8' 'xerces-c') #'ykpers')
makedepends=('git' 'zip' 'libxt')
optdeps=('xvkbd: virtual-keyboard support')
conflicts=('passwordsafe-debian' 'passwordsafe-git' 'pwsafe')
source=("http://downloads.sourceforge.net/project/$pkgname/Linux-BETA/$_pkgver/$_pkgname-$pkgver-src.tgz"
	"http://downloads.sourceforge.net/project/$pkgname/Linux-BETA/$_pkgver/$_pkgname-$pkgver-src.tgz.sig"
	"0001-1169-Don-t-buffer-dev-random-ifstream-so-as-not-to-b.patch")
install='passwordsafe.install'
md5sums=('bc65e60fb909ae67158896a3a13849b1'
         'SKIP'
         '696cd55802734a3835a88ac0cda08a5d')

prepare() {
	cd "$_pkgname-$pkgver"
	#patch -Np1 -i "$srcdir"/0001-1169-Don-t-buffer-dev-random-ifstream-so-as-not-to-b.patch
}

build() {
	cd "$_pkgname-$pkgver"
	export NO_YUBI=1
	export WX_CONFIG="/usr/bin/wx-config-2.8"

	make release help I18N
}

package() {
	cd "$_pkgname-$pkgver"

	install -Dm 755 src/ui/wxWidgets/GCCUnicodeRelease/pwsafe \
			"$pkgdir"/usr/bin/pwsafe
	install -Dm 644 install/desktop/fedora-pwsafe.desktop \
			"$pkgdir"/usr/share/applications/pwsafe.desktop
	install -Dm 644 install/graphics/pwsafe.png \
			"$pkgdir"/usr/share/icons/hicolor/48x48/apps/pwsafe.png
	
	for doc in README.txt docs/ReleaseNotes.txt docs/ChangeLog.txt install/copyright; do
		install -Dm 644 "$doc" "$pkgdir"/usr/share/doc/passwordsafe/"$doc"
	done
	
	install -Dm 644 help/help.zip "$pkgdir"/usr/share/doc/passwordsafe/help/help.zip
	install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
	install -Dm 644 docs/pwsafe.1 "$pkgdir"/usr/share/man/man1/pwsafe.1

	install -d 755 "$pkgdir"/usr/share/pwsafe/xml
	install -m 644 xml/* "$pkgdir"/usr/share/pwsafe/xml

	#for lang in src/ui/wxWidgets/I18N/mos/*; do
	#	install -Dm 644 "$lang"/LC_MESSAGES/pwsafe.mo "$pkgdir"/usr/share/locale/"$(basename "$lang")"/LC_MESSAGES/pwsafe.mo
	#done
	cd src/ui/wxWidgets/I18N/mos/
	for lang in *; do
		install -Dm 644 "$lang"/LC_MESSAGES/pwsafe.mo \
				"$pkgdir"/usr/share/locale/"$lang"/LC_MESSAGES/pwsafe.mo
	done
}

